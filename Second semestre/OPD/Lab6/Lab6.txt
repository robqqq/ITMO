ORG	0x0		; Инициализация векторов прерывания
V0:	WORD	$INT0, 0x180	; Вектор прерывания 0
V1:	WORD	$INT1, 0x180	; Вектор прерывания 1

	ORG	0x01A	; Данные
X:	WORD	?	; Переменная Х
MIN:	WORD	0xFFE8	; Левая граница ОДЗ = -24
MAX:	WORD	0x001B	; Правая граница ОДЗ + 1 = 27

START:	DI		; Запрет прерываний
	CLA		; Очистка аккумулятора
	OUT	0x1	; Запрет прерываний для ВУ-0
	OUT	0x7	; Запрет прерываний для ВУ-3
	OUT	0xB	; Запрет прерываний для ВУ-4
	OUT	0xD	; Запрет прерываний для ВУ-5
	OUT	0x11	; Запрет прерываний для ВУ-6
	OUT	0x15	; Запрет прерываний для ВУ-7
	OUT	0x19	; Запрет прерываний для ВУ-8
	OUT	0x1D	; Запрет прерываний для ВУ-9

	LD	#0x8	; Разрешение прерывания и вектор 0
	OUT	0x3	; (1000) в MR ВУ-1
	LD	#0x9	; Разрешение прерывания и вектор 1
	OUT	0x5	; (1001) в MR ВУ-2

	LD	$MIN	; Загрузка левой границы ОДЗ
	ST	$X	; Сохранение в переменную Х
	EI		; Разрешение прерываний

MAIN_LOOP:	LD	$X	; Загрузка переменной Х
	INC		; Инкремент
	DI		; Запрет прерываний
	CALL	$CHECK	; Вызов подпрограммы проверки значения Х
	EI		; Разрешение прерываний
	JUMP	MAIN_LOOP	; Продолжение цикла

INT0:	LD	$X	; Загрузка значения Х
	NOP		; Точка отладки
	ASL		; Арифметический сдвиг влево
	ASL		; Арифметический сдвиг влево
	ADD	$X	; Сложение со значением Х
	NEG		; Изменение знака
	ADD	#0x4	; Сложение с 4
	OUT	0x2	; Вывод на ВУ-1
	NOP		; Точка отладки
	IRET		; Возврат из прерывания

INT1:	IN	0x4	; Ввод с ВУ-2
	SXTB		; Расширение знака
	ADD	$X	; Сложение со значением Х
	CALL	$CHECK	; Вызов подпрограммы проверки значения Х
	NOP		; Точка отладки
	IRET		; Возврат из прерывания

CHECK:	CMP	$MIN	; Сравнение с левой границей ОДЗ
	BLT	RESET	; Переход на сброс, если меньше
	CMP	$MAX	; Сравнение с правой границей ОДЗ
	BGE	RESET	; Переход на сброс, если больше или равно
	BR	SAVE	; Переход на сохранение
RESET:	LD	$MIN	; Загрузка левой границы ОДЗ
SAVE:	ST	$X	; Сохранение в переменную Х
	RET		; Возврат из подпрограммы